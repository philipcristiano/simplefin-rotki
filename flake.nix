{
  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-25.11";
    flake-utils.url = "github:numtide/flake-utils";
    rust-overlay.url = "github:oxalica/rust-overlay";
    rust-overlay.inputs.nixpkgs.follows = "nixpkgs";
    crate2nix.url = "github:nix-community/crate2nix";
    crate2nix.inputs.nixpkgs.follows = "nixpkgs";
  };

  outputs = { self, nixpkgs, flake-utils, rust-overlay, crate2nix }:
    flake-utils.lib.eachDefaultSystem (system:
      let
        overlays = [ (import rust-overlay) ];
        pkgs = import nixpkgs {
          inherit system overlays;
        };

        package_version = pkgs.lib.removeSuffix "\n" (builtins.readFile ./VERSION);
        package_name = "simplefin-rotki";

        cargoNix = pkgs.callPackage ./Cargo.nix {};

        package = cargoNix.workspaceMembers.${package_name}.build.override {
          crateOverrides = pkgs.defaultCrateOverrides // {
            ${package_name} = attrs: {
              nativeBuildInputs = (attrs.nativeBuildInputs or []) ++ [ pkgs.tailwindcss ];
              SQLX_OFFLINE = true;
            };
          };
        };

      in with pkgs; {
        devShells.default = mkShell {
          buildInputs = [
            rust-bin.stable.latest.default
            rust-analyzer
            pkgs.crate2nix
            pkgs.tailwindcss
          ];
          shellHook = ''
            export PGDATA=$PWD/pgdata
            export PGDATABASE=et
            export PGUSER=et
          '';
        };

        packages.default = package;
        packages.docker = pkgs.dockerTools.buildLayeredImage {
          name = package_name;
          tag = package_version;
          contents = [ package pkgs.cacert ];
          config = {
            Cmd = [ "/bin/simplefin-rotki" ];
          };
        };
      }
    );
}
